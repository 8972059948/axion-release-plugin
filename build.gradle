buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath group: 'com.gradle.publish', name: 'plugin-publish-plugin', version: '0.9.1'
    }
}

plugins {
    id 'groovy'
    id 'jacoco'
    id 'pl.allegro.tech.build.axion-release' version '1.3.1'
    id 'com.github.kt3k.coveralls' version '2.3.1'
}

scmVersion {
    tag {
        prefix = 'axion-release'
    }

    hooks {
        pre 'fileUpdate', [files: ['README.md', 'docs/conf.py'], pattern: { v, p -> /'$v'/ }, replacement: { v, p -> "'$v'" }]
        pre 'commit'
    }

    versionCreator 'versionWithBranch'
}

subprojects {

    apply plugin: 'groovy'
    apply plugin: 'maven'

    if (project.hasProperty('mavenCentral')) {
        apply from: "$project.rootDir.absolutePath/gradle/mavenCentral.gradle"
    }

    group = 'pl.allegro.tech.build'
    version = scmVersion.version

    sourceCompatibility = '1.7'

    repositories {
        mavenCentral()
    }

    project.ext.versions = [
            jgit : '4.1.1.201511131810-r',
            spock: '1.0-groovy-2.4',
            cglib: '3.1'
    ]

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
