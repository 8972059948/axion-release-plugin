
plugins {
    id 'groovy'
    id 'jacoco'
    id 'nu.studer.plugindev' version '1.0.3'
    id 'pl.allegro.tech.build.axion-release' version '1.2.0'
}

scmVersion {
    tag {
        prefix = 'axion-release'
    }
    
    hooks {
        pre 'fileUpdate', [files: ['README.md', 'docs/conf.py'], pattern: {v, p -> /'$v'/ }, replacement: {v, p -> "$v"}]
        pre 'commit'
    }

    versionCreator 'versionWithBranch'
}

group = 'pl.allegro.tech.build'
version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'org.ajoberstar', name: 'grgit', version: '0.4.0'
    compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.8.0'

    testCompile(group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.1'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

plugindev {
    pluginId 'pl.allegro.tech.build.axion-release'
    pluginImplementationClass 'pl.allegro.tech.build.axion.release.ReleasePlugin'
    pluginDescription 'Release and version management plugin.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'release', 'version'
    authorId 'adamdubiel'
    authorName 'Adam Dubiel'
    authorEmail 'adam@nospam.com'
    projectUrl 'https://github.com/allegro/axion-release-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = project.ext.has('bintrayUser') ? project.ext.bintrayUser : ''
    key = project.ext.has('bintrayApiKey') ? project.ext.bintrayApiKey : ''
    pkg {
        repo = 'maven'
        version {
            gpg {
                sign = true
                passphrase = project.ext.has('gpgPassphrase') ? project.ext.gpgPassphrase : ''
            }
        }
    }
}

